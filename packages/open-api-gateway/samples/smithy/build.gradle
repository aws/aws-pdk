plugins {
    id "software.amazon.smithy" version "0.6.0"
    id "idea"
}

idea {
    module {
        sourceDirs += file("src/main/smithy")
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

// Additional Smithy dependencies can be added here
dependencies {
    implementation "software.amazon.smithy:smithy-cli:1.27.2"
    implementation "software.amazon.smithy:smithy-model:1.27.2"
    implementation "software.amazon.smithy:smithy-openapi:1.27.2"
    implementation "software.amazon.smithy:smithy-aws-traits:1.27.2"
}

// This task is called by the SmithyApiGateway project and should not be removed!
task generate(type: JavaExec) {
    configurations.implementation.setCanBeResolved(true)

    classpath = configurations.implementation
    mainClass.set('software.amazon.smithy.cli.SmithyCli')

    if (project.hasProperty("config") && project.hasProperty("output") && project.hasProperty("discover")) {
        args("build",
                "--config",
                file(project.getProperty("config")).toString(),
                "--output",
                file(project.getProperty("output")).toString(),
                "--discover",
                file(project.getProperty("discover")).toString())
    }
}
